<!DOCTYPE html>
<html lang="ja">
<head>
  <meta charset="UTF-8" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0" />
  <title>project2/サイモンゲーム/ソースコード</title>
  <link href="https://fonts.googleapis.com/css2?family=Noto+Sans+JP:wght@400;700&display=swap" rel="stylesheet">
  <link rel="stylesheet" href="project2_source.css">
</head>
<body>
  <header>
    <h1>project2</h1>
    <h2>スロットゲーム</h2>
    <h2>ソースコード</h2>
    <p>プログラマー志望 | ポートフォリオサイト</p>
  </header>
  <main class="container">
    <section class="source">
      <h2>ソースコード</h2>
	  <p>
<pre><code>
//Copyright (c) 2025 A.Matsushita. ALL rights reserved.
//Jan., 20th, 2025 Coded by Matsushita Ayato
//Jan., 20th, 2025 Added by Matsushita Ayato
#include <Wire.h>

const int DISPLAY_ADDRESS = 0x70;
const int delayTime = 300;          // 数字の回転速度
const int switchPin = A1;          // スイッチピン
const int buzzerPin = 7;           // ブザー用ピン
const int ledPins[] = {5, 6, 10, 11}; // LED用ピン

//数字のセグメント
const byte numbers[] = {
    0x3F,
    0x06,
    0x5B,
    0x4F,
    0x66,
    0x6D,
    0x7D,
    0x07,
    0x7F,
    0x6F
};

unsigned long lastLEDUpdate = 0;    // LED更新時刻
const int ledUpdateInterval = 100; // LED更新間隔
int currentLED = 0;                // 現在の点灯LED位置
bool ledDirection = true;          // LEDの動作方向

void setup() {
    Wire.begin();
    Wire.setClock(400000);

    Wire.beginTransmission(DISPLAY_ADDRESS);
    Wire.write(0x21); // 通常動作モード
    Wire.endTransmission();

    Wire.beginTransmission(DISPLAY_ADDRESS);
    Wire.write(0x81); // 表示オン
    Wire.endTransmission();

    Wire.beginTransmission(DISPLAY_ADDRESS);
    Wire.write(0xE0 | 15); // 輝度最大
    Wire.endTransmission();

    clearDisplay();

    pinMode(switchPin, INPUT_PULLUP);
    pinMode(buzzerPin, OUTPUT);

    for (int i = 0; i < 4; i++) {
        pinMode(ledPins[i], OUTPUT); // 各LEDピンを出力モードに設定
        digitalWrite(ledPins[i], LOW); // 初期状態消灯
    }
}

void loop() {
    int positions[] = {0, 1, 3, 4}; // 表示位置
    int finalNumbers[4];           // 各位置の最終結果

    for (int i = 0; i < 4; i++) {
        finalNumbers[i] = determineNumber(positions[i]); // スイッチで数字を確定
    }

    // すべての数字が揃ったかを確認
    if (checkIfAllMatch(finalNumbers)) {
        playCelebrationMusicAndEffect();
    }

    // 確定した結果を表示したまま2秒間待つ
    delay(2000);

    // すべての数字を消灯
    clearDisplay();
    delay(1000);
}

int determineNumber(int position) {
    //初期値ランダム
    int currentNumber = random(10);

    while (true) {
        // 現在の数字を表示
        displayDigit(position, currentNumber);

        // LEDウェーブ更新
        updateLEDWave();

        // スイッチが押されたら確定
        if (digitalRead(switchPin) == LOW) {
            delay(50); // デバウンス処理
            while (digitalRead(switchPin) == LOW); // スイッチが離されるのを待つ

            tone(buzzerPin, 1500, 200); // 確定音を鳴らす
            clearLEDs();                // 全LEDを消灯
            return currentNumber;       // 確定した数字を返す
        }

        // 次の数字に進む
        currentNumber = (currentNumber + 1) % 10;
        delay(delayTime);
    }
}

void displayDigit(int position, int number) {
    Wire.beginTransmission(DISPLAY_ADDRESS);
    Wire.write(position * 2);
    Wire.write(numbers[number]);
    Wire.endTransmission();
}

void clearDisplay() {
    for (int i = 0; i < 8; i++) {
        Wire.beginTransmission(DISPLAY_ADDRESS);
        Wire.write(i);
        Wire.write(0x00);
        Wire.endTransmission();
    }
}

// LEDウェーブの更新
void updateLEDWave() {
    unsigned long currentMillis = millis();
    if (currentMillis - lastLEDUpdate >= ledUpdateInterval) {
        // 現在のLEDを消灯
        digitalWrite(ledPins[currentLED], LOW);

        // 次のLEDを決定
        if (ledDirection) {
            currentLED++;
            if (currentLED >= 3) ledDirection = false;
        } else {
            currentLED--;
            if (currentLED <= 0) ledDirection = true;
        }

        // 次のLEDを点灯
        digitalWrite(ledPins[currentLED], HIGH);

        // 音の変更
        tone(buzzerPin, 1000 + currentLED * 200, 50);

        // 更新時刻記録
        lastLEDUpdate = currentMillis;
    }
}

void clearLEDs() {
    for (int i = 0; i < 4; i++) {
        digitalWrite(ledPins[i], LOW);
    }
}

bool checkIfAllMatch(int numbers[]) {
    for (int i = 1; i < 4; i++) {
        if (numbers[i] != numbers[0]) {
            return false; // 数字が一致していない
        }
    }
    return true; // すべての数字が一致
}

void playCelebrationMusicAndEffect() {
    int melody[] = {262, 294, 330, 349, 392, 440, 494, 523};
    int noteDuration = 200;

    for (int i = 0; i < 8; i++) {
        tone(buzzerPin, melody[i], noteDuration);
        waveLEDs(noteDuration);
        delay(noteDuration * 0.1); // 音間隔
    }

    clearLEDs(); //　LED消灯
}

void waveLEDs(int duration) {
    int interval = duration / 4;
    for (int i = 0; i < 4; i++) {
        digitalWrite(ledPins[i], HIGH);
        delay(interval);
        digitalWrite(ledPins[i], LOW);
    }
}

</code></pre>

    </p>
    </section>

    <section class="contact">
      <h2>お問い合わせ</h2>
      <p>メールアドレス: sss7hhh7uuu@gmail.com</p>
      <p>GitHub: <a href="https://github.com/aya72t" target="_blank">https://github.com/aya72t</a></p>
    </section>
  </main>
  <footer>
    &copy; 2025 Ayato Matsushita
  </footer>
</body>
</html>
